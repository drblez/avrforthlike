INCLUDE< device\m16def.f>
INCLUDE" delay.f"

: LCD-PORT PORTA ; INLINE-FOREVER
: LCD-PIN  PINA  ; INLINE-FOREVER
: LCD-DDR  DDRA  ; INLINE-FOREVER
: LCD-E    PA4   ; INLINE-FOREVER
: LCD-RW   PA5   ; INLINE-FOREVER
: LCD-RS   PA6   ; INLINE-FOREVER

: LCD-E-SET LCD-E LCD-PORT SET-BIT ; INLINE
: LCD-E-CLR LCD-E LCD-PORT CLR-BIT ; INLINE

: LCD-WAIT 
  $F0 LCD-DDR OUT 
  LCD-RW LCD-PORT SET-BIT
  LCD-RS LCD-PORT CLR-BIT
  BEGIN
    LCD-E-SET
    NOP NOP
    LCD-PIN IN
    LCD-E-CLR
    LCD-E-SET
    LCD-E-CLR
  4 UNTIL-BIT-CLEAR
;

: LCD-CMD ( param --> )
  LCD-WAIT
  $FF LCD-DDR OUT
  DUP
  SWAP-NIB $0F AND $10 OR LCD-PORT OUT
  LCD-E-CLR
  SWAP-NIB $0F AND $10 OR LCD-PORT OUT
  LCD-E-CLR
  $F0 LCD-DDR OUT
;
  
: LCD-CLEAR
  1 LCD-CMD 2 DELAY-MS ; INLINE
  
: LCD-DATA ( param --> )
  LCD-WAIT
  $FF LCD-DDR OUT
  DUP
  SWAP-NIB $0F AND $50 OR LCD-PORT OUT
  LCD-E-CLR
  SWAP-NIB $0F AND $50 OR LCD-PORT OUT
  LCD-E-CLR
  $F0 LCD-DDR OUT
;

: LCD-INIT
  0 LCD-PORT OUT
  $FF LCD-DDR OUT
  15 DELAY-MS
  $13 LCD-PORT OUT
  LCD-E-CLR
  5 DELAY-MS
  $13 LCD-PORT OUT
  LCD-E-CLR
  100 DELAY-MS
  $13 LCD-PORT OUT
  LCD-E-CLR
  100 DELAY-MS
  $12 LCD-PORT OUT
  LCD-E-CLR
  $F0 LCD-DDR OUT
  15 DELAY-MS
  $28 LCD-CMD 
  $08 LCD-CMD 
  LCD-CLEAR 
  $0C LCD-CMD 
  $06 LCD-CMD
;

: LCD-LINE-COL ( line col --> ) SWAP $40 * + $80 + LCD-CMD ;

: LCD-CURSOR-ON $0D LCD-CMD ;

: LCD-CURSOR-OFF $0C LCD-CMD ;

: LCD-EMIT ( c --> )
  DUP NL-CHAR =
  IF
    DROP $C0 LCD-CMD
  ELSE
    DUP FF-CHAR =
    IF
      DROP LCD-CLEAR
    ELSE
      LCD-DATA
    THEN
  THEN ;

: INIT-LCD LCD-INIT ; INIT


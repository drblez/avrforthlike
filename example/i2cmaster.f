INCLUDE" twi.f"
INCLUDE" cpu.f"

TW_READ CONSTANT I2C-READ-MODE
TW_WRITE CONSTANT I2C-WRITE-MODE

100000 QCONSTANT SCL-CLOCK
F_CPU SCL-CLOCK / 16 - 2 / CONSTANT TW-SPEED

: I2C-INIT 0 TWSR OUT TW-SPEED TWBR OUT ;

: WAIT-TWINT BEGIN TWCR IN TWINT UNTIL-BIT-CLEAR ; INLINE

: I2C-START ( addr --> succ/fail ) 
  TWINT BV TWSTA BV TWEN BV OR OR TWCR OUT WAIT-TWINT
  TW_STATUS@ DUP TW_START <> SWAP TW_REP_START <> AND IF FALSE RETURN THEN
  TWDR OUT
  TWINT BV TWEN BV OR TWCR OUT WAIT-TWINT
  TW_STATUS@ DUP TW_MT_SLA_ACK <> SWAP TW_MR_SLA_ACK <> AND IF FALSE RETURN THEN
  TRUE ;

: I2C-WRITE ( data --> succ/fail )
  TWDR OUT
  TWINT BV TWEN BV OR TWCR OUT
  WAIT-TWINT
  TW_STATUS@ TW_MT_DATA_ACK <> IF FALSE ELSE TRUE THEN ;

: I2C-STOP ( --> )
  TWINT BV TWEN BV TWSTO BV OR OR TWCR OUT
  BEGIN TWCR IN TWSTO UNTIL-BIT-CLEAR ; INLINE
  
: I2C-REP-START I2C-START ; INLINE

: I2C-READ-NACK ( --> data )
  TWINT BV TWEN BV OR TWCR OUT
  WAIT-TWINT
  TWDR IN ;

: I2C-READ-ACK ( --> data )
  TWINT BV TWEN BV OR TWEA BV OR TWCR OUT
  WAIT-TWINT
  TWDR IN ;
  
: I2CMASTER-INIT I2C-INIT ; INIT
